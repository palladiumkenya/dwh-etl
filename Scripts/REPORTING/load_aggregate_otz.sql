IF OBJECT_ID(N'[REPORTING].[dbo].[AggregateOTZ]', N'U') IS NOT NULL 
	DROP TABLE [REPORTING].[dbo].[AggregateOTZ]
GO

SELECT DISTINCT
	MFLCode,
	f.FacilityName,
	County,
	SubCounty,
	p.PartnerName,
	a.AgencyName,
	Gender,
	age.DATIMAgeGroup as AgeGroup,
	CONVERT(char(7), cast(cast(OTZEnrollmentDateKey as char) as datetime), 23) as OTZEnrollmentYearMonth,
	count(*) as Enrolled,
	-- sum(case when otz.OTZEnrollmentDateKey is null then 1 else 0 end) as NotEnrolled,
	sum(case when otz.ModulesPreviouslyCovered is not null then 1 else 0 end) as CompletedTraining,
	TransferInStatus,
	ModulesPreviouslyCovered,
	SUM(ModulesCompletedToday_OTZ_Orientation) as CompletedToday_OTZ_Orientation,
	SUM(ModulesCompletedToday_OTZ_Participation) as CompletedToday_OTZ_Participation,
	SUM(ModulesCompletedToday_OTZ_Leadership) as CompletedToday_OTZ_Leadership,
	SUM(ModulesCompletedToday_OTZ_MakingDecisions) as CompletedToday_OTZ_MakingDecisions,
	SUM(ModulesCompletedToday_OTZ_Transition) as CompletedToday_OTZ_Transition,
	SUM(ModulesCompletedToday_OTZ_TreatmentLiteracy) as CompletedToday_OTZ_TreatmentLiteracy,
	SUM(ModulesCompletedToday_OTZ_SRH) as CompletedToday_OTZ_SRH,
	SUM(ModulesCompletedToday_OTZ_Beyond) as CompletedToday_OTZ_Beyond,
	FirstVL,
	LastVL,
	SUM(vl.EligibleVL) as EligibleVL,
	ValidVLResult,
	vl.ValidVLResultCategory2 as ValidVLResultCategory,
	SUM(vl.HasValidVL) as HasValidVL,
	Count(*) TotalOTZ
INTO [REPORTING].[dbo].[AggregateOTZ]
FROM NDWH.dbo.FactOTZ otz
	INNER join NDWH.dbo.DimAgeGroup age on age.AgeGroupKey=otz.AgeGroupKey
	INNER join NDWH.dbo.DimFacility f on f.FacilityKey = otz.FacilityKey
	INNER JOIN NDWH.dbo.DimAgency a on a.AgencyKey = otz.AgencyKey
	INNER JOIN NDWH.dbo.DimPatient pat on pat.PatientKey = otz.PatientKey
	INNER JOIN NDWH.dbo.DimPartner p on p.PartnerKey = otz.PartnerKey
	LEFT JOIN NDWH.dbo.FactViralLoads vl on vl.PatientKey = otz.PatientKey and vl.PatientKey IS NOT NULL
	WHERE age.Age BETWEEN 10 AND 24 AND IsTXCurr = 1
	GROUP BY 
		MFLCode, 
		f.FacilityName, 
		County, 
		SubCounty, 
		p.PartnerName,
		a.AgencyName, 
		Gender, 
		age.DATIMAgeGroup, 
		CONVERT(char(7), cast(cast(OTZEnrollmentDateKey as char) as datetime), 23), 
		TransferInStatus, 
		ModulesPreviouslyCovered, 
		vl.FirstVL, 
		vl.LastVL, 
		vl.ValidVLResult,
		vl.ValidVLResultCategory2
